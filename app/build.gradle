apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'maven-publish'

android {
    namespace 'ai.elimu.chat'
    compileSdk 35

    defaultConfig {
        applicationId "ai.elimu.chat"
        minSdkVersion 26
        targetSdkVersion 35
        versionCode 2000001
        versionName "2.0.1-SNAPSHOT"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError false
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.3.1'
    }
}

greendao {
    schemaVersion android.defaultConfig.versionCode
    targetGenDir = new File('app/src/main/java')
    daoPackage = android.defaultConfig.applicationId + '.dao'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'ai.elimu:model:model-2.0.97' // https://jitpack.io/#ai.elimu/model
    implementation 'ai.elimu:content-provider:1.2.38@aar' // https://jitpack.io/#ai.elimu/content-provider
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

publishing {
    publications {
        apk(MavenPublication) {
            artifact("${buildDir}/outputs/apk/release/app-release-unsigned.apk")
        }
    }
}
tasks.named("publishApkPublicationToMavenLocal") {
    mustRunAfter(":app:assembleRelease")
}
